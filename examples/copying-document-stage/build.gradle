apply plugin: 'java-library'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://artifactory.lucidworks.com/artifactory/public-artifacts/"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.version = '0.0.1'

configurations {
    provided // configuration for dependencies that are already present in Fusion
    implementation.extendsFrom(provided)
}

dependencies {
    // Index Stage SDK dependency is required for all plugins, version must be compatible with your Fusion deployment
    provided "com.lucidworks-plugins.index-stage-sdk:index-stage-plugin-sdk:1.+"

    testImplementation "junit:junit:4.13"
    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation "org.slf4j:slf4j-simple:1.7.30"
    testImplementation "com.lucidworks-plugins.index-stage-sdk:index-stage-sdk-test:1.+"
}

// specific task that allows to create proper plugin zip file
task assemblePlugin(type: Jar) {
    group("build")
    getArchiveExtension().set("zip")
    manifest {
        attributes 'Plugin-Id': 'copying-plugin'
        attributes 'Plugin-Version': "${project.version}"
        attributes 'Plugin-SDK-Version': '1.1.0'
        attributes 'Plugin-Base-Package': 'com.lucidworks.sample.copying'
    }
    into('lib') {
        from configurations.runtimeClasspath - configurations.provided
        from jar
    }
}
